= Valid Parenthesis | 7kyu Codewars Challenge
:icons: font
:sectlinks:
:sectnums!:
:toclevels: 6
:toc: left
:favicon: https://fernandobasso.dev/cmdline.png

* link:https://www.codewars.com/kata/6411b91a5e71b915d237332d[Valid Parenthesis 7kyu Codewars challenge^]

In the comments, people say this problem should bin in the 6kyu category.

Examples:

----
""                //=> true
"("               //=> false
"(("              //=> false
")(()))"          //=> false
"()"              //=> true
"()()"            //=> true
"((()))"          //=> true
"())()("          //=> false
"(())((()())())"  //=> true
----

== JavaScript

=== v1

[source,javascript]
----
/**
 * Recursive solution using the run/go helper function approach.
 *
 * - T.C: O(n).
 * - S.C: O(n).
 *
 * @param {string}
 * @returns {number}
 */
function validParens(s) {
  return (function go(lst, l, r) {
    if (lst.length === 0) return l === r;

    if (lst[0] === '(') ++l;
    if (lst[0] === ')') ++r;

    if (r > l) return false;

    return go(lst.slice(1), l, r);
  })([...s], 0, 0);
}
----

=== v2

[source,js]
----
/**
 * @param {string}
 * @returns {number}
 */
function validParens(s) {
  return (function go(l, c) {
    if (l.length === 0) return c === 0;

    if (l[0] === '(') ++c;
    if (l[0] === ')') --c;

    if (c < 0) return false;

    return go(l.slice(1), c);
  })([...s], 0);
}
----

== Haskell

=== v1

[source,haskell]
----
incIf :: Char -> Char -> Int -> Int
incIf c1 c2 n
  | c1 == c2 = (+ 1) n
  | otherwise = n

validParens :: String -> Bool
validParens str = go str 0 0
  where
    go :: String -> Int -> Int -> Bool
    go s l r
      | length s == 0 = l == r
      | r > l = False
      | otherwise = go
                    (tail s)
                    (incIf (head s) '(' l)
                    (incIf (head s) ')' r)
----

=== v2

Improvements suggested by _@Morrow_ on the link:https://discord.com/channels/280033776820813825/505367988166197268/1117783688504156200[Haskell chat on Discord^].

[quote, Morrow, Haskell Discord chat]
____
In `go`, instead of checking if the length is 0 and then using head and tail later, consider pattern matching instead (which also allows GHC to warn you about missing patterns).
____


[source,haskell]
----
incIf :: Char -> Char -> Int -> Int
incIf c1 c2 n
  | c1 == c2 = (+ 1) n
  | otherwise = n

validParens :: String -> Bool
validParens str = go str 0 0
  where
    go :: String -> Int -> Int -> Bool
    go [] l r = l == r
    go (h : t) l r
      | r > l = False
      | otherwise = go
                    t
                    (incIf h '(' l)
                    (incIf h ')' r)
----

=== v3 by MorrowM

link:https://discord.com/channels/280033776820813825/505367988166197268/1118113731356926037[Discord Chat about Morrow's solution^].
link:https://www.codewars.com/kata/reviews/6419c913bfedda0001da3a2c/groups/648709aa74a2f700013c7c1a[Morrow's solution in Codewars^].

[quote, Morrow, Discord Haskell Chat]
____
I use a technique where you combine `foldr` with CPS, so the accumulator is actually a function (which is why it looks like `go` has an extra argument).
This allows it to be both efficient and short circuit, while still being able to use `foldr`, which has its own potential efficiency benefits.
____

[source,haskell]
----
validParentheses :: String -> Bool
validParentheses s = foldr go (== 0) s 0
  where
    go _ _ n | n < 0 = False
    go '(' k n = k (n + 1)
    go ')' k n = k (n - 1)
    go _ k n = k n -- not strictly necessary
----

Haskell has truly come from an advanced alien civilization.

=== v4

This was a solution I saw from user _monadius_:

[source,haskell]
----
validParens :: String -> Bool
validParens str = (== 0) count
  where count = foldr
                (\c n ->
                    if n < 0 then n
                    else if c == '('
                         then n - 1
                         else n + 1) 0 str
----

I could not make sense of it then changed it a little bit and asked on link:https://discord.com/channels/280033776820813825/505367988166197268/1117771069521154048[Discord Haskell server^].

Basically, this is `foldr`, which means the `n` input will be passed the result of folding the tail.

If it was `foldl`` then the n input would be the accumulated result for everything folded over so far, but since it is `foldr , it is instead passed the result of the tail, which means whether the lambda accumulates `n` has no bearing of what `n` will be during the folding of the tail.

It works from beginning to the end of the input string, building up the computation, and then the results propagate back up.

Let's denote that big lambda by `go` and evaluate the code step by step (explanation by @Morrow):

[source,text]
----
validParens "(()"
 = foldr go 0 "(()"
 = go '(' (foldr go 0 "()")
 = let n0 = foldr go 0 "()"
   in if n0 < 0
        then n0
        else
          if '(' == ')'
          then n0 + 1
          else n0 - 1
= ...
= let n0 =
    let n1 =
      let n2 = foldr go 0 ""
      in if n2 < 0
        then n2
        else
          if ')' == ')'
          then n2 + 1
          else n2 - 1
    in if n1 < 0
      then n1
      else
        if '(' == ')'
        then n1 + 1
        else n1 - 1
  in if n0 < 0
        then n0
        else
          if '(' == ')'
          then n0 + 1
          else n0 - 1
----

So we've built up this big gnarly expression as we've traversed the list, but now we can begin to evaluate it since n2 = foldr go 0 "" evaluates to 0. So

[source,text]
----
if n2 < 0
then n2
else
  if ')' == ')'
  then n2 + 1
  else n2 - 1
----

evaluates to the else branch which evaluates to `n2 + 1` which is 1, so `n1 = 1`

Evaluate `n0` to get `n0 = n1 - 1 = 0`.
Evaluate the final ‘in’ to get a final result of `n0 - 1 = -1`.

Now you can try to do this sort of evaluation for the input `"()"`

But you do see that the computation effectively goes from the back of the list to the head of the list, like `l0ne` said.

link:https://www.codewars.com/kata/reviews/6419c913bfedda0001da3a2c/groups/648709aa74a2f700013c7c1a[Here's a solution^] by @Morrow.
